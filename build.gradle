buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
    }
}

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'eclipse'
    id 'idea'
    id "com.jfrog.bintray" version "1.7.3"
}

version = "2.1.0"
description = "${project.name} build script"
group = "benjaminkomen"

sourceCompatibility = JavaVersion.VERSION_13
targetCompatibility = JavaVersion.VERSION_13

repositories {
    jcenter()
    mavenLocal()
    mavenCentral()
}

dependencies {
    api "com.google.code.gson:gson:2.8.6"
    api "com.squareup.okhttp3:okhttp:4.2.2"


    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.5.2'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.5.2'
//    testRuntimeOnly 'org.junit.platform:junit-platform-launcher:1.3.2'

    testImplementation 'com.squareup.okhttp3:mockwebserver:4.2.2'
    api 'org.slf4j:slf4j-api:1.8.0-beta2'
    compileOnly 'org.projectlombok:lombok:1.18.4'
    annotationProcessor 'org.projectlombok:lombok:1.18.4'
}

test {
    useJUnitPlatform()
}


javadoc {
    source = sourceSets.main.allJava
    classpath = configurations.compileClasspath

    options {
        setMemberLevel JavadocMemberLevel.PUBLIC
        setAuthor true

        links "https://docs.oracle.com/en/java/javase/13/docs/api/"
        links "https://square.github.io/okio/1.x/okio/"
        links "https://square.github.io/okhttp/3.x/okhttp/"
        links "https://fastily.github.io/jwiki/docs/gson/"

        // links "http://square.github.io/okhttp/3.x/mockwebserver/"
        // setOutputLevel JavadocOutputLevel.VERBOSE
    }
}


task sourceJar(type: Jar) {
    from sourceSets.main.allJava
    classifier = 'sources'
}

task packageJavadoc(type: Jar) {
    from javadoc
    classifier = 'javadoc'
}


task wgen(type: Jar) {
    archiveBaseName = "WGen"

    from {
        configurations.compile.collect { it.isDirectory() ? it : zipTree(it) }
    }

    manifest {
        attributes 'Main-Class': 'benjaminkomen.jwiki.util.WGen'
    }

    exclude "META-INF/*.SF", "**/LICENSE*", "**/NOTICE*", "**/maven/**"

    with jar
}


wrapper {
    gradleVersion = '6.0.1'
}


publishing {
    publications {
        mavenPublish(MavenPublication) {
            from components.java

            artifact sourceJar {
                classifier "sources"
            }
            artifact packageJavadoc {
                classifier "javadoc"
            }

            groupId 'benjaminkomen'
            artifactId 'jwiki'
            version project.version

            pom {
                name = 'jwiki'
                description = 'A library for interacting with Wikia/MediaWiki'
                inceptionYear = '2014'
                url = 'https://github.com/benjaminkomen/jwiki'
                licenses {
                    license {
                        name = 'GNU General Public License, Version 3.0'
                        url = 'https://www.gnu.org/licenses/gpl-3.0.txt'
                        distribution = 'repo'
                    }
                }
            }
        }
    }
}

apply from: 'bintray.gradle'